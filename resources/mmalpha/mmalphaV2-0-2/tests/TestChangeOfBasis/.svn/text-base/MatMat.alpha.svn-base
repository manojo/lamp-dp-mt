system MatVect :{N,M | 2<=N; 2<=M}
               (A : {i,j | 1<=i<=N; 1<=j<=M} of real; 
                b : {i | 1<=i<=M} of real)
       returns (c : {i | 1<=i<=N} of real);
var
  C : {i,j | 1<=i<=N; 0<=j<=M} of real;
let
  C[i,j] = 
      case
        { | j=0} : 0[];
        { | 1<=j} : C[i,j-1] + A[i,j] * b[j];
      esac;
  c[i] = C[i,M];
tel;

system MatMat :{N,M,P | 2<=N; 2<=M; 2<=P}
               (A : {i,j | 1<=i<=N; 1<=j<=M} of real; 
                B : {i,j | 1<=i<=M; 1<=j<=P} of real)
       returns (C : {i,k | 1<=i<=N; 1<=k<=P} of real);
var
  Abis : {i,j,k | 1<=i<=N; 1<=j<=M; 1<=k<=P} of real;
  VectIN : {i,k | 1<=i<=M; 1<=k<=P} of real;
  VectOUT : {j,k | 1<=j<=N; 1<=k<=P} of real;
let
  Abis[i,j,k] = A[i,j];
  VectIN[i,k] = B[i,k];
  use {k | 1<=k<=P} MatVect[N,M] (Abis, VectIN) returns  (VectOUT) ;
  C[i,k] = VectOUT[i,k];
tel;
